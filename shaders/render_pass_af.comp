#version 430

/*
in uvec3 gl_NumWorkGroups;
in uvec3 gl_WorkGroupID;
in uvec3 gl_LocalInvocationID;
in uvec3 gl_GlobalInvocationID;
in uint gl_LocalInvocationIndex;
*/

struct Ant {
  float position_x;
  float position_y;

  float direction_x;
  float direction_y;

  uint has_food;
  uint is_alive;

  uint anthill_position_x;
  uint anthill_position_y;
};

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(binding = 1, r8ui) uniform uimage2D af_tex;
layout(binding = 2, r32f) uniform image2D pher_tex;
layout(binding = 3, rgba32f) uniform image2D render_tex;
layout(location = 1) uniform float delta_time;

void main() {
  ivec2 coord = ivec2(gl_GlobalInvocationID.xy);

  // To avoid running too many instances that run on the entire image, we
  // do the pheromone decay here.
  // It decays by 0.01*current_value*delta_time.
  float pheromone_value = imageLoad(pher_tex, coord).r;
  pheromone_value -= 0.01 * pheromone_value * delta_time;
  imageStore(pher_tex, coord, vec4(pheromone_value));

  // Food value is stored in the first 7 bits of the af_tex
  uint food_value = imageLoad(af_tex, coord).r & 0x7F;
  vec3 food_color = vec3(.569, .921, .149) * (food_value / 128.0);

  bool anthill = (imageLoad(af_tex, coord).r & 0x80) != 0;
  vec3 anthill_color = vec3(.921, .149, .149) * float(anthill);

  vec3 pheromone_color = vec3(0.0, .5, 0.5) * pheromone_value / 255.0;

  imageStore(render_tex, coord,
             vec4(food_color + anthill_color + pheromone_color, 1.0));
}